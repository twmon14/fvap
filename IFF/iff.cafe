mod! PRINCIPAL {
  [Prin]
  op enemy : -> Prin
  eq (P:Prin = P) = true .
}

mod! KEY {
  pr(PRINCIPAL)
  [Key]
  op k : Prin -> Key
  op p : Key -> Prin
  var P : Prin
  vars K1 K2 : Key
  eq p(k(P)) = P .
  ceq (K1 = K2) = true if not(p(K1) = enemy) and not(p(K2) = enemy) .
  ceq (K1 = K2) = false if not(p(K1) = enemy) and p(K2) = enemy .
}

mod! RAND {
  [Rand]
}

mod! CIPHER {
  pr(PRINCIPAL + KEY + RAND)
  [Cipher]
  op enc : Key Rand Prin -> Cipher
  op k : Cipher -> Key
  op r : Cipher -> Rand
  op p : Cipher -> Prin
  var K : Key
  var R : Rand
  var P : Prin
  vars C1 C2 : Cipher
  eq k(enc(K,R,P)) = K .
  eq r(enc(K,R,P)) = R .
  eq p(enc(K,R,P)) = P .
  eq (C1 = C2) = (k(C1) = k(C2) and r(C1) = r(C2) and p(C1) = p(C2)) .
}

mod! MSG {
  pr(PRINCIPAL + RAND + CIPHER)
  [Msg]
  --      creator source destination body
  --      ---------------------------------
  op cm : Prin    Prin   Prin        Rand   -> Msg
  op rm : Prin    Prin   Prin        Cipher -> Msg
  --
  op cm? : Msg -> Bool
  op rm? : Msg -> Bool
  op crt : Msg -> Prin
  op src : Msg -> Prin
  op dst : Msg -> Prin
  op r : Msg -> Rand
  op c : Msg -> Cipher
  --
  vars P1 P2 P3 : Prin
  var R : Rand
  var C : Cipher
  vars M1 M2 : Msg
  --
  eq cm?(cm(P1,P2,P3,R)) = true .
  eq cm?(rm(P1,P2,P3,C)) = false .
  eq rm?(cm(P1,P2,P3,R)) = false .
  eq rm?(rm(P1,P2,P3,C)) = true .
  eq crt(cm(P1,P2,P3,R)) = P1 .
  eq crt(rm(P1,P2,P3,C)) = P1 .
  eq src(cm(P1,P2,P3,R)) = P2 .
  eq src(rm(P1,P2,P3,C)) = P2 .
  eq dst(cm(P1,P2,P3,R)) = P3 .
  eq dst(rm(P1,P2,P3,C)) = P3 .
  eq r(cm(P1,P2,P3,R)) = R .
  eq c(rm(P1,P2,P3,C)) = C .
  --
  ceq (M1 = M2) = (cm?(M1) and crt(M1) = crt(M2) and src(M1) = src(M2) and dst(M1) = dst(M2) and r(M1) = r(M2)) if cm?(M2) .
  ceq (M1 = M2) = (rm?(M1) and crt(M1) = crt(M2) and src(M1) = src(M2) and dst(M1) = dst(M2) and c(M1) = c(M2)) if rm?(M2) .
}

mod! BAG (D :: TRIV) {
  [Elt.D < Bag]
  op void : -> Bag
  op _,_ : Bag Bag -> Bag { assoc comm id: void }
  op _\in_ : Elt.D Bag -> Bool
  var B : Bag
  vars E1 E2 : Elt.D
  eq E1 \in void = false .
  ceq E1 \in (E2,B) = true if E1 = E2 .
  ceq E1 \in (E2,B) = E1 \in B if not(E1 = E2) .
}

mod! SET (D :: TRIV) {
  [Elt.D < Set]
  op empty : -> Set
  op __ : Set Set -> Set { assoc comm idem id: empty }
  op _\in_ : Elt.D Set -> Bool
  var S : Set
  vars E1 E2 : Elt.D
  eq E1 \in empty = false .
  ceq E1 \in (E2 S) = true if E1 = E2 .
  ceq E1 \in (E2 S) = E1 \in S if not(E1 = E2) .
}

mod! COLLECTION(D :: TRIV) {
  [Elt.D < Col]
  op _\in_ : Elt.D Col -> Bool
}

view TRIV2MSG from TRIV to MSG {
  sort Elt -> Msg
}
view TRIV2CIPHER from TRIV to CIPHER {
  sort Elt -> Cipher
}
view TRIV2RAND from TRIV to RAND {
  sort Elt -> Rand
}

mod! NETWORK {
  pr(BAG(D <= TRIV2MSG)*{ sort Bag -> Network })
  pr(COLLECTION(D <= TRIV2RAND)*{sort Col -> ColRands})
  pr(COLLECTION(D <= TRIV2CIPHER)*{sort Col -> ColCiphers})
--
  op rands : Network -> ColRands
  op ciphers : Network -> ColCiphers
--
  var NW : Network
  var M : Msg
  var R : Rand
  var C : Cipher
-- rands
  eq R \in rands(void) = false .
  ceq R \in rands(M,NW) = true if cm?(M) and R = r(M) .
  ceq R \in rands(M,NW) = true if rm?(M) and k(enemy) = k(c(M)) and R = r(c(M)) .
  ceq R \in rands(M,NW) = R \in rands(NW)
      if not(cm?(M) and R = r(M)) and
         not(rm?(M) and k(enemy) = k(c(M)) and R = r(c(M))) .
-- ciphers
  eq C \in ciphers(void) = false .
  ceq C \in ciphers(M,NW) = true if rm?(M) and C = c(M) .
  ceq C \in ciphers(M,NW) = C \in ciphers(NW) if not(rm?(M) and C = c(M)) .

-- lemmas, add by @duongtd 21/01/16
  op lm1 : Cipher Cipher ColCiphers -> Bool .
  var C' : Cipher .
  var CS : ColCiphers .
  eq lm1(C, C', CS) = (C = C' and C \in CS implies C' \in CS) .
}

--
-- Identify-Friend-or-Foe System
--
mod* IFF {
  pr(NETWORK)
  pr(SET(RAND)*{sort Set -> URands})
  [Sys]
-- an arbitrary initial state
  op init : -> Sys {constr}
-- observations
  op nw : Sys -> Network
  op ur : Sys -> URands
-- actions
  op sdcm : Sys Prin Prin Rand -> Sys {constr}
  op sdrm : Sys Prin Msg -> Sys {constr}
  --
  op fkcm1 : Sys Prin Prin Rand -> Sys {constr}
  op fkrm1 : Sys Prin Prin Cipher -> Sys {constr}
  op fkrm2 : Sys Prin Prin Rand -> Sys {constr}

-- CafeOBJ variables
  var S : Sys
  vars P1 P2 P3 : Prin
  vars M1 M2 : Msg
  var R : Rand
  var C : Cipher
  var K : Key

-- for any initial state
  eq nw(init) = void .
  eq ur(init) = empty .

-- for action sdcm
  op c-sdcm : Sys Prin Prin Rand -> Bool
  eq c-sdcm(S,P1,P2,R) = (not (R \in ur(S))) .
  --
  ceq nw(sdcm(S,P1,P2,R)) = (cm(P1,P1,P2,R) , nw(S)) if c-sdcm(S,P1,P2,R) .
  ceq ur(sdcm(S,P1,P2,R)) = (R ur(S))                if c-sdcm(S,P1,P2,R) .
  ceq sdcm(S,P1,P2,R)     = S                      if not c-sdcm(S,P1,P2,R) .

-- for action sdrm
  op c-sdrm : Sys Prin Msg -> Bool
  eq c-sdrm(S,P1,M1) = (M1 \in nw(S) and cm?(M1) and P1 = dst(M1)) .
  --
  ceq nw(sdrm(S,P1,M1)) = rm(P1,P1,src(M1),enc(k(P1),r(M1),P1)) , nw(S) if c-sdrm(S,P1,M1) .
  eq ur(sdrm(S,P1,M1))  = ur(S) .
  ceq sdrm(S,P1,M1)     = S                                             if not c-sdrm(S,P1,M1) .

-- for action fkcm1
  op c-fkcm1 : Sys Prin Prin Rand -> Bool
  eq c-fkcm1(S,P1,P2,R) = R \in rands(nw(S)) .
  --
  ceq nw(fkcm1(S,P1,P2,R)) = cm(enemy,P1,P2,R) , nw(S) if c-fkcm1(S,P1,P2,R) .
  eq ur(fkcm1(S,P1,P2,R))  = ur(S) .
  ceq fkcm1(S,P1,P2,R)     = S                         if not c-fkcm1(S,P1,P2,R) .

-- for action fkrm1
  op c-fkrm1 : Sys Prin Prin Cipher -> Bool
  eq c-fkrm1(S,P1,P2,C) = C \in ciphers(nw(S)) .
  --
  ceq nw(fkrm1(S,P1,P2,C)) = rm(enemy,P1,P2,C) , nw(S) if c-fkrm1(S,P1,P2,C) .
  eq ur(fkrm1(S,P1,P2,C))  = ur(S) .
  ceq fkrm1(S,P1,P2,C)     = S                         if not c-fkrm1(S,P1,P2,C) .

-- for action fkrm2
  op c-fkrm2 : Sys Prin Prin Rand -> Bool
  eq c-fkrm2(S,P1,P2,R) = R \in rands(nw(S)) .
  --
  ceq nw(fkrm2(S,P1,P2,R)) = rm(enemy,P1,P2,enc(k(enemy),R,P1)) , nw(S) if c-fkrm2(S,P1,P2,R) .
  eq ur(fkrm2(S,P1,P2,R))  = ur(S) .
  ceq fkrm2(S,P1,P2,R)     = S                       if not c-fkrm2(S,P1,P2,R) .

-- INV
-- declare invariant candidates
  op inv1 : Sys Prin Prin Prin Key Rand -> Bool
  op inv2 : Sys Key Rand -> Bool

-- CafeOBJ variables
  var S : Sys
  vars P1 P2 P3 : Prin
  var K : Key
  var R : Rand

-- define invariant candidates
  eq inv1(S,P1,P2,P3,K,R) = ((not(K = k(enemy)) and rm(P1,P2,P3,enc(K,R,P2)) \in nw(S)) implies not(P2 = enemy)) .
  eq inv2(S,K,R) = (enc(K,R,enemy) \in ciphers(nw(S)) implies (K = k(enemy))) .

}
